# Maintainer: Johan Reitan <johan.reitan@gmail.com>
pkgname=qtlogout-git
_pkgname=qtLogout
pkgver=r10.d018bcd
pkgrel=1
pkgdesc="Qt exit app for openbox, compiz, pekwm, etc."
arch=('i686' 'x86_64')
url="https://github.com/joharei/qtLogout"
license=('GPL3')
depends=('qt5-base' 'liblxqt')
makedepends=('git' 'cmake')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
replaces=()
source=('git+https://github.com/joharei/qtLogout.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/${_pkgname%-git}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/${_pkgname%-git}"
    [[ -d build ]] && rm -r build
    mkdir build && cd build
    cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      ..
    make
}

package() {
	cd "$srcdir/${_pkgname%-git}/build"
	make DESTDIR="$pkgdir/" install
}
